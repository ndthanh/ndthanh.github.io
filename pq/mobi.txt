let

    fn = (intPage as number) as table =>

    let
        myHeaders = [
        #"Accept"="application/json, text/plain, */*",
        #"Accept-Language"="en-US,en;q=0.9",
        #"Connection"="keep-alive",
        #"Cookie"=Excel.CurrentWorkbook(){[Name="paramCookie"]}[Content]{0}[cookie],
        #"Referer"="https://congtacvien.mobifone.vn/admin/sim-so/danh-sach-sim-so.html",
        #"Sec-Fetch-Dest"="empty",
        #"Sec-Fetch-Mode"="cors",
        #"Sec-Fetch-Site"="same-origin",
        #"User-Agent"="Mozilla/5.0 (Macintosh; Intel Mac OS X 10_15_7) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/114.0.0.0 Safari/537.36"
        ],
    
        myURL = "https://congtacvien.mobifone.vn/admin/sim-so/tim-kiem?camket=&dauso=010&khuvuc=7&kieuso=&loaithuebao=-1&so=&tinhtrang=&intPage=" & Text.From(intPage),
        Source = Json.Document(Web.Contents(myURL, [Headers=myHeaders])),
        items = Source[items],
        #"Converted to Table" = Table.FromList(items, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
        #"Expanded Column1" = Table.ExpandRecordColumn(#"Converted to Table", "Column1", {"msisdn", "status", "type", "groupId", "dwType", "groupName", "minPrice", "blockTime", "symbol", "priceConnect"}, {"msisdn", "status", "type", "groupId", "dwType", "groupName", "minPrice", "blockTime", "symbol", "priceConnect"})
    in
        #"Expanded Column1",

fnType = type function
    (intPage as number) as table meta
    [
        Documentation.Name = "fnGetNumbers",
        Documentation.LongDescription = "This function retrieves data from a specific page of the 'https://congtacvien.mobifone.vn/admin/sim-so/tim-kiem' API, converts the 'items' field of the JSON response into a table, and expands the table to include all fields of the 'items'. Author: Nguyen Duc Thanh. Website: https://blog.hocexcel.online. YouTube: https://youtube.com/hocexcelonline",
        Documentation.Examples =  {[
            Description = "Returns a table with data retrieved from the first page of the API",
            Code = "fnGetNumbers(1)",
            Result = "#table({""msisdn"", ""status"", ""type"", ""groupId"", ""dwType"", ""groupName"", ""minPrice"", ""blockTime"", ""symbol"", ""priceConnect""}, { ... })"  // Replace with actual result
        ]}
    ]
    in
    Value.ReplaceType(fn, fnType)
